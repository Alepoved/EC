/*-----------------------------------------------------------------
**
**  Fichero:
**    init.asm  10/6/2014
**
**    Estructura de Computadores
**    Dpto. de Arquitectura de Computadores y Automática
**    Facultad de Informática. Universidad Complutense de Madrid
**
**  Propósito:
**    Arranca un programa en C
**
**  Notas de diseño:
**
**---------------------------------------------------------------*/

    .extern main
    .extern ISR_Undef
	.extern ISR_SWI
	.extern ISR_Pabort
	.extern ISR_Dabort
	.extern ISR_IRQ
/*	.extern ISR_FIQ */
	.extern DoDetecta

	.global start
	
	.global DoUndef
    .global DoSWI
    .global DoDabort

/* Tabla en la que escribir las direcciones de las ISRs */
.equ 	_ISR_STARTADDRESS,	0xc7fff00		/* GCS6:64M DRAM/SDRAM 	*/
.equ    HandleReset,    _ISR_STARTADDRESS
.equ    HandleUndef,    _ISR_STARTADDRESS+4
.equ    HandleSWI,      _ISR_STARTADDRESS+4*2
.equ    HandlePabort,   _ISR_STARTADDRESS+4*3
.equ    HandleDabort,   _ISR_STARTADDRESS+4*4
.equ    HandleReserved, _ISR_STARTADDRESS+4*5
.equ    HandleIRQ,      _ISR_STARTADDRESS+4*6
.equ    HandleFIQ,      _ISR_STARTADDRESS+4*7

/* Constantes utiles para la rutina ISR_FIQ */
.equ INTPND, 0x1e00004
.equ EXTINTPND, 0x1d20054
.equ F_ISPC, 0x1e0003c

	/*
	** Modos de operación
	*/
	.equ MODEMASK, 0x1f		/* Para selección de M[4:0] del CPSR */
	.equ USRMODE,  0x10
	.equ FIQMODE,  0x11
	.equ IRQMODE,  0x12
	.equ SVCMODE,  0x13
	.equ ABTMODE,  0x17
	.equ UNDMODE,  0x1b
	.equ SYSMODE,  0x1f

	/*
	** Direcciones de las bases de las pilas del sistema 
	*/
	.equ USRSTACK, 0xc7ff000   	
	.equ SVCSTACK, 0xc7ff100
	.equ UNDSTACK, 0xc7ff200
	.equ ABTSTACK, 0xc7ff300
	.equ IRQSTACK, 0xc7ff400
	.equ FIQSTACK, 0xc7ff500

	/*
	** Registro de máscara de interrupción
	*/
	.equ rINTMSK,    0x1e0000c

start:

	/* Pasa a modo supervisor */
    mrs	r0, cpsr
    bic	r0, r0, #MODEMASK
    orr	r1, r0, #SVCMODE
    msr	cpsr_c, r1 

	/* Enmascara interrupciones */
	ldr r0, =rINTMSK
	ldr r1, =0x1fffffff
    str r1, [r0]

	/* Habilita linea IRQ y FIQ del CPSR */
	mrs r0, cpsr
	bic r0, r0, #0xC0
	msr cpsr_c, r0
	
	/* Desde modo SVC inicializa los SP de todos los modos de ejecución privilegiados */
    bl InitStacks
	
	/* Esscribe en la tabla de ISR las direcciones de las rutinas de tratamiento de excepcion*/
	bl InitISR

	/* Desde modo SVC cambia a modo USR e inicializa el SP_usr */
	mrs r0, cpsr
	bic r0, r0, #MODEMASK
	orr r1, r0, #USRMODE  
	msr cpsr_c, r1
	ldr sp, =USRSTACK

    mov fp, #0

    bl main

End:
    B End

InitStacks:
	mrs r0, cpsr
	bic r0, r0, #MODEMASK

	orr r1, r0, #UNDMODE  /* desde modo SVC cambia a modo UND e inicializa el SP_und */
	msr cpsr_c, r1    
	ldr sp, =UNDSTACK

	orr r1, r0, #ABTMODE  /* desde modo UND cambia a modo ABT e inicializa el SP_abt */
	msr cpsr_c, r1 
	ldr sp, =ABTSTACK

	orr r1, r0, #IRQMODE  /* desde modo ABT cambia a modo IRQ e inicializa el SP_abt */
	msr cpsr_c, r1
	ldr sp, =IRQSTACK

	orr r1, r0, #FIQMODE  /* desde modo IRQ cambia a modo FIQ e inicializa el SP_fiq */
	msr cpsr_c, r1
	ldr sp, =FIQSTACK

	orr r1, r0, #SVCMODE  /* desde modo FIQ cambia a modo SVC e inicializa el SP_svc */
	msr cpsr_c, r1
	ldr sp, =SVCSTACK
	
    mov pc, lr


InitISR:
    /* CÛdigo de la primera parte */
    ldr r0,=ISR_Undef
    ldr r1,=HandleUndef
    str r0,[r1]

    ldr r0,=ISR_Dabort
    ldr r1,=HandleDabort
    str r0,[r1]

    ldr r0,=ISR_Pabort
    ldr r1,=HandlePabort
    str r0,[r1]

    ldr r0,=ISR_IRQ
    ldr r1,=HandleIRQ
    str r0,[r1]

    ldr r0,=ISR_SWI
    ldr r1,=HandleSWI
    str r0,[r1]

    ldr r0,=ISR_FIQ
    ldr r1,=HandleFIQ
    str r0,[r1]

	mov pc,lr


ISR_FIQ:
		push {r0-r3,r11,r12,lr}
		add fp,sp,#24
		ldr r0,=INTPND
		ldr r0,[r0]
		and r0,r0,#0x0200000 // check bit 21
		cmp r0,#0
		beq fin_irq
		ldr r0,=EXTINTPND
		ldr r0,[r0]
		and r0,r0,#0x0C // check bits 2 y3
		cmp r0,#0
		beq fin_irq
		bl DoDetecta
fin_irq: ldr r0,=F_ISPC
		mov r1,#0x0200000
		str r1,[r0]
		sub sp,fp,#24
		pop {r0-r3,r11,r12,lr}
		subs pc,lr,#4





DoSWI:
	swi #0
	mov pc,lr

DoUndef:
	.word 0xE6000010
	mov pc,lr

DoDabort:
	ldr r0,=0x0a333333
	str r0,[r0]
	mov pc,lr


	.end

